plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    task generateSourcesJar(type: Jar) {
        archiveClassifier.set( 'sources')
        from sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator)) + configurations.compile
    }

    javadoc {
        options {
            encoding "UTF-8"
            charSet 'UTF-8'
            author true
            version true
            links "http://docs.oracle.com/javase/8/docs/api"
        }
    }

    task generateJavadoc(type: Jar, dependsOn:['javadoc']) {
        group = 'jar'
        archiveClassifier.set('javadoc')
        from 'build/docs/javadoc'
    }

    task generateMergeJar(type: Jar, dependsOn:['assembleRelease']){
        group = 'jar'
        from(project.zipTree('build/intermediates/aar_main_jar/release/classes.jar'))
    }
}

dependencies {
    compile 'com.android.support:support-annotations:28.0.0'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'io.github.xiaozeiqwe8' //开通maven central时候定义的
            artifactId 'mavendemo-lib' //资源名称
            version '0.9.0' //版本名称
            pom {
                name = 'mavendemo-lib'
                description = 'mavendemo lib'
                url = 'https://github.com/xiaozeiqwe8/mavendemo'
                inceptionYear = '2021'

                scm {
                    url = 'https://github.com/xiaozeiqwe8/mavendemo'
                    connection = 'scm:git@https://github.com/xiaozeiqwe8/mavendemo.git'
                    developerConnection = 'scm:git@https://github.com/xiaozeiqwe8/mavendemo.git'
                }

                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                        comments = 'A business-friendly OSS license'
                    }
                }

                developers {
                    developer {
                        id = 'xiaozei'
                        name = 'karl'
                        email = 'xiaozeiqwe8@163.com'
                        url = 'https://github.com/xiaozeiqwe8/mavendemo'
                    }
                }

                issueManagement {
                    system = 'Github'
                    url = 'https://github.com/xiaozeiqwe8/mavendemo'
                }
            }
            artifact generateSourcesJar //需要上传的source jar
            artifact generateJavadoc //需要上传的java doc
            artifact generateMergeJar //需要上传的资源jar路径或者是aar路径，这边可以填写一个生成jar的task，如makeJar方法就是一个生成jar的task
        }
    }
    repositories {
        maven {
            // 指定要上传的maven仓库
            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            //Maven仓库用户名和密码
            credentials {
                username MAVEN_USERNAME
                password MAVEN_PASSWORD
            }
        }
    }
}
signing {
    sign publishing.publications.maven //签名配置
}